buildscript {
    ext {
        springCloudVersion = 'Edgware.SR3'
        springBootVersion = '1.5.14.RELEASE'
        axonVersion = '3.1.2'
        aspectjVersion = '1.9.1'
        verifierVersion = '1.2.3.RELEASE'
    }

    dependencies {
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifierVersion}"
        classpath("com.jayway.restassured:rest-assured:2.5.0")
        classpath("com.jayway.restassured:spring-mock-mvc:2.5.0")
    }
}

plugins {
    id "io.spring.dependency-management" version "1.0.5.RELEASE"
    id "org.springframework.boot" version "1.5.14.RELEASE" apply false
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

apply plugin: 'java'

dependencies {
    compile project('customers')
    compile project('account.cmd')
    compile project('account.query')
    compile project('transactions')
    compile project('edgeservice')
    testCompile ('com.jayway.restassured:rest-assured:2.5.0')
    testCompile ('com.jayway.restassured:spring-mock-mvc:2.5.0')
}

assemble.dependsOn(clean)

def springProjects = subprojects.findAll { project -> project.name.indexOf('cmd') != -1 || project.name.indexOf('query') != -1}
def supportProjects = subprojects.findAll { project -> project.name.equals('edgeservice') }
def crudProjects = subprojects.findAll { project -> project.name.equals('customers') }

springProjects.addAll(supportProjects)
springProjects.addAll(crudProjects)
springProjects.add(project('transactions'))

allprojects {
    group = 'com.ultimatesoftware.banking'
    sourceCompatibility = 1.8

	repositories {
		mavenCentral()
        maven {
            url 'https://dl.bintray.com/palantir/releases'
        }
	}
    apply plugin: 'java'
    apply plugin: 'checkstyle'

    checkstyle {
        configFile = new File(rootDir, "checkstyles.xml")
    }

    checkstyleTest {
        source = fileTree('src/test') {
            excludes = ['*.yml', '*contracts/org/springframework/cloud/contract/verifier/tests*']
        }
    }
}

configure(springProjects) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'groovy'
    apply plugin: 'spring-cloud-contract'

    configurations {
        providedRuntime
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
    dependencies {
        compile 'org.springframework.boot:spring-boot-starter-actuator'
        compile 'org.springframework.boot:spring-boot-starter-aop'
        compile 'org.springframework.cloud:spring-cloud-starter-config'

        testCompile 'junit:junit:4.12'
        testCompile('org.springframework.boot:spring-boot-starter-test')
        testCompile "org.springframework.cloud:spring-cloud-starter-contract-verifier"
        testCompile "org.springframework.cloud:spring-cloud-contract-spec"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8'
}

task printProj {
    println 'Projects: ' + springProjects
}

assemble.dependsOn(clean)
